
// This verifies the potential multiplicative reduction at 2 of the Frey hyperelliptic curve for K.

K<z> := QuadraticField(5);
OK := Integers(K);
I2 := Factorization(2*OK)[1,1];

F<a,c> := FieldOfFractions(PolynomialRing(K,3));

R<x> := PolynomialRing(F);

b := c^5 - a;
f := x^5 - 5*c^2*x^3 + 5*c^4*x - 2*(a-b);

C := HyperellipticCurve(f);
JI := JInvariants(C);

// a has weight 5
// c has weight 1
// the degree of the J-invariants are thus 4, 8, 12, 16, 20
 
LI := 
[
    700*c^4,
    13750*c^8,
    1280000*a^2*c^2 - 1280000*a*c^7 + 312500*c^12,
    224000000*a^2*c^6 - 224000000*a*c^11 + 7421875*c^16,
    204800000*a^4 - 409600000*a^3*c^5 + 204800000*a^2*c^10
];

assert JI eq LI;
assert LI[1]^5/LI[5] eq 5^5 * 7^5 * c^20/(2^6 * a^2 * (a - c^5)^2);


